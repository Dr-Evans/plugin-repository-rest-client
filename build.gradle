plugins {
  id "org.jetbrains.kotlin.jvm"
  id "com.jfrog.bintray"
}

allprojects {
  apply plugin: 'java'

  repositories {
    mavenCentral()
    maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
  }
}

subprojects {
  apply plugin: 'kotlin'

  sourceCompatibility = '1.6'
  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile 'org.slf4j:slf4j-api:1.7.26'
  }
}

task fatJar(type: Jar, group: 'build') {
  subprojects {
    dependsOn it.classes
  }
  manifest {
    attributes 'Main-Class': 'org.jetbrains.intellij.PluginRepository.Client'
  }
  subprojects { project ->
    from { project.configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
  }
}

group = 'org.jetbrains.intellij'
def buildNumber = System.getenv('BUILD_NUMBER') ?: "SNAPSHOT"
version = "2.0.$buildNumber"

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
  subprojects {
    from it.sourceSets.main.kotlin
  }
}
artifacts {
  archives sourceJar, fatJar
}
publishing {
  publications {
    mavenJava(MavenPublication) {
      from project(':services:plugin-repository-rest-client:rest').components.java
      artifact sourceJar {
        classifier "sources"
      }
      artifact fatJar {
        classifier "all"
      }
    }
  }
}
if (hasProperty("bintrayUser")) {
  bintray {
    user = bintrayUser
    key = bintrayApiKey
    publish = true
    publications = ['mavenJava']
    pkg {
      userOrg = "jetbrains"
      repo = "intellij-plugin-service"
      name = "plugin-repository-rest-client"
      version {
          name = project.version
      }
    }
  }
}
